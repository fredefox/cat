-- | Univalent categories
--
-- This module defines:
--
-- Categories
-- ==========
--
-- Types
-- ------
--
-- Object, Arrow
--
-- Data
-- ----
-- identity; the identity arrow
-- _âˆ˜_; function composition
--
-- Laws
-- ----
--
-- associativity, identity, arrows form sets, univalence.
--
-- Lemmas
-- ------
--
-- Propositionality for all laws about the category.
{-# OPTIONS --allow-unsolved-metas --cubical #-}

module Cat.Category where

open import Cat.Prelude
open import Cat.Equivalence as Equivalence renaming (_â‰…_ to _â‰ˆ_ ; Isomorphism to TypeIsomorphism) hiding (preorderâ‰…)

import Function

------------------
-- * Categories --
------------------

-- | Raw categories
--
-- This record desribes the data that a category consist of as well as some laws
-- about these. The laws defined are the types the propositions - not the
-- witnesses to them!
record RawCategory (â„“a â„“b : Level) : Set (lsuc (â„“a âŠ” â„“b)) where
  no-eta-equality
  field
    Object   : Set â„“a
    Arrow    : Object â†’ Object â†’ Set â„“b
    identity : {A : Object} â†’ Arrow A A
    _âˆ˜_      : {A B C : Object} â†’ Arrow B C â†’ Arrow A B â†’ Arrow A C

  infixl 10 _âˆ˜_ _>>>_

  -- | Operations on data

  domain : {a b : Object} â†’ Arrow a b â†’ Object
  domain {a} _ = a

  codomain : {a b : Object} â†’ Arrow a b â†’ Object
  codomain {b = b} _ = b

  _>>>_ : {A B C : Object} â†’ (Arrow A B) â†’ (Arrow B C) â†’ Arrow A C
  f >>> g = g âˆ˜ f

  -- | Laws about the data

  -- FIXME It seems counter-intuitive that the normal-form is on the
  -- right-hand-side.
  IsAssociative : Set (â„“a âŠ” â„“b)
  IsAssociative = âˆ€ {A B C D} {f : Arrow A B} {g : Arrow B C} {h : Arrow C D}
    â†’ h âˆ˜ (g âˆ˜ f) â‰¡ (h âˆ˜ g) âˆ˜ f

  IsIdentity : ({A : Object} â†’ Arrow A A) â†’ Set (â„“a âŠ” â„“b)
  IsIdentity id = {A B : Object} {f : Arrow A B}
    â†’ id âˆ˜ f â‰¡ f Ã— f âˆ˜ id â‰¡ f

  ArrowsAreSets : Set (â„“a âŠ” â„“b)
  ArrowsAreSets = âˆ€ {A B : Object} â†’ isSet (Arrow A B)

  IsInverseOf : âˆ€ {A B} â†’ (Arrow A B) â†’ (Arrow B A) â†’ Set â„“b
  IsInverseOf = Î» f g â†’ g âˆ˜ f â‰¡ identity Ã— f âˆ˜ g â‰¡ identity

  Isomorphism : âˆ€ {A B} â†’ (f : Arrow A B) â†’ Set â„“b
  Isomorphism {A} {B} f = Î£[ g âˆˆ Arrow B A ] IsInverseOf f g

  _â‰…_ : (A B : Object) â†’ Set â„“b
  _â‰…_ A B = Î£[ f âˆˆ Arrow A B ] (Isomorphism f)

  module _ {A B : Object} where
    Epimorphism : {X : Object } â†’ (f : Arrow A B) â†’ Set â„“b
    Epimorphism {X} f = (gâ‚€ gâ‚ : Arrow B X) â†’ gâ‚€ âˆ˜ f â‰¡ gâ‚ âˆ˜ f â†’ gâ‚€ â‰¡ gâ‚

    Monomorphism : {X : Object} â†’ (f : Arrow A B) â†’ Set â„“b
    Monomorphism {X} f = (gâ‚€ gâ‚ : Arrow X A) â†’ f âˆ˜ gâ‚€ â‰¡ f âˆ˜ gâ‚ â†’ gâ‚€ â‰¡ gâ‚

  IsInitial  : Object â†’ Set (â„“a âŠ” â„“b)
  IsInitial  I = {X : Object} â†’ isContr (Arrow I X)

  IsTerminal : Object â†’ Set (â„“a âŠ” â„“b)
  IsTerminal T = {X : Object} â†’ isContr (Arrow X T)

  Initial  : Set (â„“a âŠ” â„“b)
  Initial  = Î£ Object IsInitial

  Terminal : Set (â„“a âŠ” â„“b)
  Terminal = Î£ Object IsTerminal

  -- | Univalence is indexed by a raw category as well as an identity proof.
  module Univalence (isIdentity : IsIdentity identity) where
    -- | The identity isomorphism
    idIso : (A : Object) â†’ A â‰… A
    idIso A = identity , identity , isIdentity

    -- | Extract an isomorphism from an equality
    --
    -- [HoTT Â§9.1.4]
    idToIso : (A B : Object) â†’ A â‰¡ B â†’ A â‰… B
    idToIso A B eq = transp (\ i â†’ A â‰… eq i) (idIso A)

    Univalent : Set (â„“a âŠ” â„“b)
    Univalent = {A B : Object} â†’ isEquiv (A â‰¡ B) (A â‰… B) (idToIso A B)

    import Cat.Equivalence as E
    open E public using () renaming (Isomorphism to TypeIsomorphism)

    univalenceFromIsomorphism : {A B : Object}
      â†’ TypeIsomorphism (idToIso A B) â†’ isEquiv (A â‰¡ B) (A â‰… B) (idToIso A B)
    univalenceFromIsomorphism = fromIso _ _

    -- A perhaps more readable version of univalence:
    Univalentâ‰ƒ = {A B : Object} â†’ (A â‰¡ B) â‰ƒ (A â‰… B)

    -- | Equivalent formulation of univalence.
    Univalent[Contr] : Set _
    Univalent[Contr] = âˆ€ A â†’ isContr (Î£[ X âˆˆ Object ] A â‰… X)

    -- From: Thierry Coquand <Thierry.Coquand@cse.gu.se>
    -- Date: Wed, Mar 21, 2018 at 3:12 PM
    --
    -- This is not so straight-forward so you can assume it
    postulate from[Contr] : Univalent[Contr] â†’ Univalent

module _ {â„“a â„“b : Level} (â„‚ : RawCategory â„“a â„“b) where
  record IsPreCategory : Set (lsuc (â„“a âŠ” â„“b)) where
    open RawCategory â„‚ public
    field
      isAssociative : IsAssociative
      isIdentity    : IsIdentity identity
      arrowsAreSets : ArrowsAreSets
    open Univalence isIdentity public

    leftIdentity : {A B : Object} {f : Arrow A B} â†’ identity âˆ˜ f â‰¡ f
    leftIdentity {A} {B} {f} = fst (isIdentity {A = A} {B} {f})

    rightIdentity : {A B : Object} {f : Arrow A B} â†’ f âˆ˜ identity â‰¡ f
    rightIdentity {A} {B} {f} = snd (isIdentity {A = A} {B} {f})

    ------------
    -- Lemmas --
    ------------

    -- | Relation between iso- epi- and mono- morphisms.
    module _ {A B : Object} {X : Object} (f : Arrow A B) where
      isoâ†’epi : Isomorphism f â†’ Epimorphism {X = X} f
      isoâ†’epi (f- , left-inv , right-inv) gâ‚€ gâ‚ eq = begin
        gâ‚€              â‰¡âŸ¨ sym rightIdentity âŸ©
        gâ‚€ âˆ˜ identity   â‰¡âŸ¨ cong (_âˆ˜_ gâ‚€) (sym right-inv) âŸ©
        gâ‚€ âˆ˜ (f âˆ˜ f-)   â‰¡âŸ¨ isAssociative âŸ©
        (gâ‚€ âˆ˜ f) âˆ˜ f-   â‰¡âŸ¨ cong (Î» Ï† â†’ Ï† âˆ˜ f-) eq âŸ©
        (gâ‚ âˆ˜ f) âˆ˜ f-   â‰¡âŸ¨ sym isAssociative âŸ©
        gâ‚ âˆ˜ (f âˆ˜ f-)   â‰¡âŸ¨ cong (_âˆ˜_ gâ‚) right-inv âŸ©
        gâ‚ âˆ˜ identity   â‰¡âŸ¨ rightIdentity âŸ©
        gâ‚              âˆ

      isoâ†’mono : Isomorphism f â†’ Monomorphism {X = X} f
      isoâ†’mono (f- , left-inv , right-inv) gâ‚€ gâ‚ eq =
        begin
        gâ‚€            â‰¡âŸ¨ sym leftIdentity âŸ©
        identity âˆ˜ gâ‚€ â‰¡âŸ¨ cong (Î» Ï† â†’ Ï† âˆ˜ gâ‚€) (sym left-inv) âŸ©
        (f- âˆ˜ f) âˆ˜ gâ‚€ â‰¡âŸ¨ sym isAssociative âŸ©
        f- âˆ˜ (f âˆ˜ gâ‚€) â‰¡âŸ¨ cong (_âˆ˜_ f-) eq âŸ©
        f- âˆ˜ (f âˆ˜ gâ‚) â‰¡âŸ¨ isAssociative âŸ©
        (f- âˆ˜ f) âˆ˜ gâ‚ â‰¡âŸ¨ cong (Î» Ï† â†’ Ï† âˆ˜ gâ‚) left-inv âŸ©
        identity âˆ˜ gâ‚ â‰¡âŸ¨ leftIdentity âŸ©
        gâ‚            âˆ

      isoâ†’epiÃ—mono : Isomorphism f â†’ Epimorphism {X = X} f Ã— Monomorphism {X = X} f
      isoâ†’epiÃ—mono iso = isoâ†’epi iso , isoâ†’mono iso

    propIsAssociative : isProp IsAssociative
    propIsAssociative = propPiImpl (Î» _ â†’ propPiImpl (Î» _ â†’ propPiImpl (Î» _ â†’ propPiImpl (Î» _ â†’ propPiImpl (Î» _ â†’ propPiImpl (Î» _ â†’ propPiImpl Î» _ â†’ arrowsAreSets _ _))))))

    propIsIdentity : âˆ€ {f : âˆ€ {A} â†’ Arrow A A} â†’ isProp (IsIdentity f)
    propIsIdentity = propPiImpl (Î» _ â†’ propPiImpl Î» _ â†’ propPiImpl (Î» _ â†’ propSig (arrowsAreSets _ _) Î» _ â†’ arrowsAreSets _ _))

    propArrowIsSet : isProp (âˆ€ {A B} â†’ isSet (Arrow A B))
    propArrowIsSet = propPiImpl Î» _ â†’ propPiImpl (Î» _ â†’ isSetIsProp)

    propIsInverseOf : âˆ€ {A B f g} â†’ isProp (IsInverseOf {A} {B} f g)
    propIsInverseOf = propSig (arrowsAreSets _ _) (Î» _ â†’ arrowsAreSets _ _)

    module _ {A B : Object} {f : Arrow A B} where
      isoIsProp : isProp (Isomorphism f)
      isoIsProp a@(g , Î· , Îµ) a'@(g' , Î·' , Îµ') =
        lemSig (Î» g â†’ propIsInverseOf) a a' geq
          where
            geq : g â‰¡ g'
            geq = begin
              g             â‰¡âŸ¨ sym rightIdentity âŸ©
              g âˆ˜ identity  â‰¡âŸ¨ cong (Î» Ï† â†’ g âˆ˜ Ï†) (sym Îµ') âŸ©
              g âˆ˜ (f âˆ˜ g')  â‰¡âŸ¨ isAssociative âŸ©
              (g âˆ˜ f) âˆ˜ g'  â‰¡âŸ¨ cong (Î» Ï† â†’ Ï† âˆ˜ g') Î· âŸ©
              identity âˆ˜ g' â‰¡âŸ¨ leftIdentity âŸ©
              g'            âˆ

    propIsInitial : âˆ€ I â†’ isProp (IsInitial I)
    propIsInitial I x y i {X} = res X i
      where
      module _ (X : Object) where
        open Î£ (x {X}) renaming (fst to fx ; snd to cx)
        open Î£ (y {X}) renaming (fst to fy ; snd to cy)
        fp : fx â‰¡ fy
        fp = cx fy
        prop : (x : Arrow I X) â†’ isProp (âˆ€ f â†’ x â‰¡ f)
        prop x = propPi (Î» y â†’ arrowsAreSets x y)
        cp : (Î» i â†’ âˆ€ f â†’ fp i â‰¡ f) [ cx â‰¡ cy ]
        cp = lemPropF prop fp
        res : (fx , cx) â‰¡ (fy , cy)
        res i = fp i , cp i

    propIsTerminal : âˆ€ T â†’ isProp (IsTerminal T)
    propIsTerminal T x y i {X} = res X i
      where
      module _ (X : Object) where
        open Î£ (x {X}) renaming (fst to fx ; snd to cx)
        open Î£ (y {X}) renaming (fst to fy ; snd to cy)
        fp : fx â‰¡ fy
        fp = cx fy
        prop : (x : Arrow X T) â†’ isProp (âˆ€ f â†’ x â‰¡ f)
        prop x = propPi (Î» y â†’ arrowsAreSets x y)
        cp : (Î» i â†’ âˆ€ f â†’ fp i â‰¡ f) [ cx â‰¡ cy ]
        cp = lemPropF prop fp
        res : (fx , cx) â‰¡ (fy , cy)
        res i = fp i , cp i

    module _ where
      private
        transâ‰… : Transitive _â‰…_
        transâ‰… (f , f~ , f-inv) (g , g~ , g-inv)
          = g âˆ˜ f
          , f~ âˆ˜ g~
          , ( begin
              (f~ âˆ˜ g~) âˆ˜ (g âˆ˜ f) â‰¡âŸ¨ isAssociative âŸ©
              (f~ âˆ˜ g~) âˆ˜ g âˆ˜ f â‰¡âŸ¨ cong (Î» Ï† â†’ Ï† âˆ˜ f) (sym isAssociative) âŸ©
              f~ âˆ˜ (g~ âˆ˜ g) âˆ˜ f â‰¡âŸ¨ cong (Î» Ï† â†’ f~ âˆ˜ Ï† âˆ˜ f) (fst g-inv) âŸ©
              f~ âˆ˜ identity âˆ˜ f â‰¡âŸ¨ cong (Î» Ï† â†’ Ï† âˆ˜ f) rightIdentity âŸ©
              f~ âˆ˜ f           â‰¡âŸ¨ fst f-inv âŸ©
              identity âˆ
            )
          , ( begin
              g âˆ˜ f âˆ˜ (f~ âˆ˜ g~) â‰¡âŸ¨ isAssociative âŸ©
              g âˆ˜ f âˆ˜ f~ âˆ˜ g~ â‰¡âŸ¨ cong (Î» Ï† â†’ Ï† âˆ˜ g~) (sym isAssociative) âŸ©
              g âˆ˜ (f âˆ˜ f~) âˆ˜ g~ â‰¡âŸ¨ cong (Î» Ï† â†’ g âˆ˜ Ï† âˆ˜ g~) (snd f-inv) âŸ©
              g âˆ˜ identity âˆ˜ g~ â‰¡âŸ¨ cong (Î» Ï† â†’ Ï† âˆ˜ g~) rightIdentity âŸ©
              g âˆ˜ g~ â‰¡âŸ¨ snd g-inv âŸ©
              identity âˆ
            )
        isPreorder : IsPreorder _â‰…_
        isPreorder = record { isEquivalence = equalityIsEquivalence ; reflexive = idToIso _ _ ; trans = transâ‰… }

      preorderâ‰… : Preorder _ _ _
      preorderâ‰… = record { Carrier = Object ; _â‰ˆ_ = _â‰¡_ ; _âˆ¼_ = _â‰…_ ; isPreorder = isPreorder }

  record PreCategory : Set (lsuc (â„“a âŠ” â„“b)) where
    field
      isPreCategory  : IsPreCategory
    open IsPreCategory isPreCategory public

  -- Definition 9.6.1 in [HoTT]
  record StrictCategory : Set (lsuc (â„“a âŠ” â„“b)) where
    field
      preCategory : PreCategory
    open PreCategory preCategory
    field
      objectsAreSets : isSet Object

  record IsCategory : Set (lsuc (â„“a âŠ” â„“b)) where
    field
      isPreCategory : IsPreCategory
    open IsPreCategory isPreCategory public
    field
      univalent : Univalent

    -- | The formulation of univalence expressed with _â‰ƒ_ is trivially admissable -
    -- just "forget" the equivalence.
    univalentâ‰ƒ : Univalentâ‰ƒ
    univalentâ‰ƒ = _ , univalent

    module _ {A B : Object} where
      iso-to-id : (A â‰… B) â†’ (A â‰¡ B)
      iso-to-id = fst (toIso _ _ univalent)

    -- | All projections are propositions.
    module Propositionality where
      propUnivalent : isProp Univalent
      propUnivalent a b i = propPi (Î» iso â†’ propIsContr) a b i

      -- | Terminal objects are propositional - a.k.a uniqueness of terminal
      -- | objects.
      --
      -- Having two terminal objects induces an isomorphism between them - and
      -- because of univalence this is equivalent to equality.
      propTerminal : isProp Terminal
      propTerminal Xt Yt = res
        where
        open Î£ Xt renaming (fst to X ; snd to Xit)
        open Î£ Yt renaming (fst to Y ; snd to Yit)
        open Î£ (Xit {Y}) renaming (fst to Yâ†’X) using ()
        open Î£ (Yit {X}) renaming (fst to Xâ†’Y) using ()
        -- Need to show `left` and `right`, what we know is that the arrows are
        -- unique. Well, I know that if I compose these two arrows they must give
        -- the identity, since also the identity is the unique such arrow (by X
        -- and Y both being terminal objects.)
        Xprop : isProp (Arrow X X)
        Xprop f g = trans (sym (snd Xit f)) (snd Xit g)
        Yprop : isProp (Arrow Y Y)
        Yprop f g = trans (sym (snd Yit f)) (snd Yit g)
        left : Yâ†’X âˆ˜ Xâ†’Y â‰¡ identity
        left = Xprop _ _
        right : Xâ†’Y âˆ˜ Yâ†’X â‰¡ identity
        right = Yprop _ _
        iso : X â‰… Y
        iso = Xâ†’Y , Yâ†’X , left , right
        fromIso' : X â‰… Y â†’ X â‰¡ Y
        fromIso' = fst (toIso (X â‰¡ Y) (X â‰… Y) univalent)
        p0 : X â‰¡ Y
        p0 = fromIso' iso
        p1 : (Î» i â†’ IsTerminal (p0 i)) [ Xit â‰¡ Yit ]
        p1 = lemPropF propIsTerminal p0
        res : Xt â‰¡ Yt
        res i = p0 i , p1 i

      -- Merely the dual of the above statement.

      propInitial : isProp Initial
      propInitial Xi Yi = res
        where
        open Î£ Xi renaming (fst to X ; snd to Xii)
        open Î£ Yi renaming (fst to Y ; snd to Yii)
        open Î£ (Xii {Y}) renaming (fst to Yâ†’X) using ()
        open Î£ (Yii {X}) renaming (fst to Xâ†’Y) using ()
        -- Need to show `left` and `right`, what we know is that the arrows are
        -- unique. Well, I know that if I compose these two arrows they must give
        -- the identity, since also the identity is the unique such arrow (by X
        -- and Y both being terminal objects.)
        Xprop : isProp (Arrow X X)
        Xprop f g = trans (sym (snd Xii f)) (snd Xii g)
        Yprop : isProp (Arrow Y Y)
        Yprop f g = trans (sym (snd Yii f)) (snd Yii g)
        left : Yâ†’X âˆ˜ Xâ†’Y â‰¡ identity
        left = Yprop _ _
        right : Xâ†’Y âˆ˜ Yâ†’X â‰¡ identity
        right = Xprop _ _
        iso : X â‰… Y
        iso = Yâ†’X , Xâ†’Y , right , left
        fromIso' : X â‰… Y â†’ X â‰¡ Y
        fromIso' = fst (toIso (X â‰¡ Y) (X â‰… Y) univalent)
        p0 : X â‰¡ Y
        p0 = fromIso' iso
        p1 : (Î» i â†’ IsInitial (p0 i)) [ Xii â‰¡ Yii ]
        p1 = lemPropF propIsInitial p0
        res : Xi â‰¡ Yi
        res i = p0 i , p1 i

module _ {â„“a â„“b : Level} (â„‚ : RawCategory â„“a â„“b) where
  open RawCategory â„‚
  open Univalence
  private
    module _ (x y : IsPreCategory â„‚) where
      module x = IsPreCategory x
      module y = IsPreCategory y
      -- In a few places I use the result of propositionality of the various
      -- projections of `IsCategory` - Here I arbitrarily chose to use this
      -- result from `x : IsCategory C`. I don't know which (if any) possibly
      -- adverse effects this may have.
      -- module Prop = X.Propositionality

      propIsPreCategory : x â‰¡ y
      IsPreCategory.isAssociative (propIsPreCategory i)
        = x.propIsAssociative x.isAssociative y.isAssociative i
      IsPreCategory.isIdentity    (propIsPreCategory i)
        = x.propIsIdentity x.isIdentity y.isIdentity i
      IsPreCategory.arrowsAreSets (propIsPreCategory i)
        = x.propArrowIsSet x.arrowsAreSets y.arrowsAreSets i

    module _ (x y : IsCategory â„‚) where
      module X = IsCategory x
      module Y = IsCategory y
      -- In a few places I use the result of propositionality of the various
      -- projections of `IsCategory` - Here I arbitrarily chose to use this
      -- result from `x : IsCategory C`. I don't know which (if any) possibly
      -- adverse effects this may have.
      module Prop = X.Propositionality

      isIdentity : (Î» _ â†’ IsIdentity identity) [ X.isIdentity â‰¡ Y.isIdentity ]
      isIdentity = X.propIsIdentity X.isIdentity Y.isIdentity

      U : âˆ€ {a : IsIdentity identity}
        â†’ (Î» _ â†’ IsIdentity identity) [ X.isIdentity â‰¡ a ]
        â†’ (b : Univalent a)
        â†’ Set _
      U eqwal univ =
        (Î» i â†’ Univalent (eqwal i))
        [ X.univalent â‰¡ univ ]
      P : (y : IsIdentity identity)
        â†’ (Î» _ â†’ IsIdentity identity) [ X.isIdentity â‰¡ y ] â†’ Set _
      P y eq = âˆ€ (univ : Univalent y) â†’ U eq univ
      p : âˆ€ (b' : Univalent X.isIdentity)
        â†’ (Î» _ â†’ Univalent X.isIdentity) [ X.univalent â‰¡ b' ]
      p univ = Prop.propUnivalent X.univalent univ
      helper : P Y.isIdentity isIdentity
      helper = pathJ P p Y.isIdentity isIdentity
      eqUni : U isIdentity Y.univalent
      eqUni = helper Y.univalent
      done : x â‰¡ y
      IsCategory.isPreCategory (done i)
        = propIsPreCategory X.isPreCategory Y.isPreCategory i
      IsCategory.univalent     (done i) = eqUni i

  propIsCategory : isProp (IsCategory â„‚)
  propIsCategory = done


-- | Univalent categories
--
-- Just bundles up the data with witnesses inhabiting the propositions.

-- Question: Should I remove the type `Category`?
record Category (â„“a â„“b : Level) : Set (lsuc (â„“a âŠ” â„“b)) where
  field
    raw            : RawCategory â„“a â„“b
    {{isCategory}} : IsCategory raw

  open IsCategory isCategory public

-- The fact that being a category is a mere proposition gives rise to this
-- equality principle for categories.
module _ {â„“a â„“b : Level} {â„‚ ğ”» : Category â„“a â„“b} where
  private
    module â„‚ = Category â„‚
    module ğ”» = Category ğ”»

  module _ (rawEq : â„‚.raw â‰¡ ğ”».raw) where
    private
      isCategoryEq : (Î» i â†’ IsCategory (rawEq i)) [ â„‚.isCategory â‰¡ ğ”».isCategory ]
      isCategoryEq = lemPropF propIsCategory rawEq

    Categoryâ‰¡ : â„‚ â‰¡ ğ”»
    Category.raw (Categoryâ‰¡ i) = rawEq i
    Category.isCategory (Categoryâ‰¡ i) = isCategoryEq i

-- | Syntax for arrows- and composition in a given category.
module _ {â„“a â„“b : Level} (â„‚ : Category â„“a â„“b) where
  open Category â„‚
  _[_,_] : (A : Object) â†’ (B : Object) â†’ Set â„“b
  _[_,_] = Arrow

  _[_âˆ˜_] : {A B C : Object} â†’ (g : Arrow B C) â†’ (f : Arrow A B) â†’ Arrow A C
  _[_âˆ˜_] = _âˆ˜_

-- | The opposite category
--
-- The opposite category is the category where the direction of the arrows are
-- flipped.
module Opposite {â„“a â„“b : Level} where
  module _ (â„‚ : Category â„“a â„“b) where
    private
      module _ where
        module â„‚ = Category â„‚
        opRaw : RawCategory â„“a â„“b
        RawCategory.Object   opRaw = â„‚.Object
        RawCategory.Arrow    opRaw = Function.flip â„‚.Arrow
        RawCategory.identity opRaw = â„‚.identity
        RawCategory._âˆ˜_      opRaw = Function.flip â„‚._âˆ˜_

        open RawCategory opRaw

        isIdentity : IsIdentity identity
        isIdentity = swap â„‚.isIdentity

        isPreCategory : IsPreCategory opRaw
        IsPreCategory.isAssociative isPreCategory = sym â„‚.isAssociative
        IsPreCategory.isIdentity isPreCategory = isIdentity
        IsPreCategory.arrowsAreSets isPreCategory = â„‚.arrowsAreSets

      open IsPreCategory isPreCategory

      module _ {A B : â„‚.Object} where
        k : Equivalence.Isomorphism (â„‚.idToIso A B)
        k = toIso _ _ â„‚.univalent
        open Î£ k renaming (fst to f ; snd to inv)
        open AreInverses inv

        _âŠ™_ = Function._âˆ˜_
        infixr 9 _âŠ™_

        -- f    : A â„‚.â‰… B â†’ A â‰¡ B
        flipDem : A â‰… B â†’ A â„‚.â‰… B
        flipDem (f , g , inv) = g , f , inv

        flopDem : A â„‚.â‰… B â†’ A â‰… B
        flopDem (f , g , inv) = g , f , inv

        flipInv : âˆ€ {x} â†’ (flipDem âŠ™ flopDem) x â‰¡ x
        flipInv = refl

        -- Shouldn't be necessary to use `arrowsAreSets` here, but we have it,
        -- so why not?
        lem : (p : A â‰¡ B) â†’ idToIso A B p â‰¡ flopDem (â„‚.idToIso A B p)
        lem p i = lâ‰¡r i
          where
          l = idToIso A B p
          r = flopDem (â„‚.idToIso A B p)
          open Î£ l renaming (fst to l-obv ; snd to l-areInv)
          open Î£ l-areInv renaming (fst to l-invs ; snd to l-iso)
          open Î£ l-iso renaming (fst to l-l ; snd to l-r)
          open Î£ r renaming (fst to r-obv ; snd to r-areInv)
          open Î£ r-areInv renaming (fst to r-invs ; snd to r-iso)
          open Î£ r-iso renaming (fst to r-l ; snd to r-r)
          l-obvâ‰¡r-obv : l-obv â‰¡ r-obv
          l-obvâ‰¡r-obv = refl
          l-invsâ‰¡r-invs : l-invs â‰¡ r-invs
          l-invsâ‰¡r-invs = refl
          l-lâ‰¡r-l : l-l â‰¡ r-l
          l-lâ‰¡r-l = â„‚.arrowsAreSets _ _ l-l r-l
          l-râ‰¡r-r : l-r â‰¡ r-r
          l-râ‰¡r-r = â„‚.arrowsAreSets _ _ l-r r-r
          lâ‰¡r : l â‰¡ r
          lâ‰¡r i = l-obvâ‰¡r-obv i , l-invsâ‰¡r-invs i , l-lâ‰¡r-l i , l-râ‰¡r-r i

        ff : A â‰… B â†’ A â‰¡ B
        ff = f âŠ™ flipDem

        -- inv : AreInverses (â„‚.idToIso A B) f
        invv : AreInverses (idToIso A B) ff
        -- recto-verso : â„‚.idToIso A B âˆ˜ f â‰¡ idFun (A â„‚.â‰… B)
        invv = record
          { verso-recto = funExt (Î» x â†’ begin
            (ff âŠ™ idToIso A B) x                       â‰¡âŸ¨âŸ©
            (f  âŠ™ flipDem âŠ™ idToIso A B) x             â‰¡âŸ¨ cong (Î» Ï† â†’ Ï† x) (cong (Î» Ï† â†’ f âŠ™ flipDem âŠ™ Ï†) (funExt lem)) âŸ©
            (f  âŠ™ flipDem âŠ™ flopDem âŠ™ â„‚.idToIso A B) x â‰¡âŸ¨âŸ©
            (f  âŠ™ â„‚.idToIso A B) x                     â‰¡âŸ¨ (Î» i â†’ verso-recto i x) âŸ©
            x âˆ)
          ; recto-verso = funExt (Î» x â†’ begin
            (idToIso A B âŠ™ f âŠ™ flipDem) x             â‰¡âŸ¨ cong (Î» Ï† â†’ Ï† x) (cong (Î» Ï† â†’ Ï† âŠ™ f âŠ™ flipDem) (funExt lem)) âŸ©
            (flopDem âŠ™ â„‚.idToIso A B âŠ™ f âŠ™ flipDem) x â‰¡âŸ¨ cong (Î» Ï† â†’ Ï† x) (cong (Î» Ï† â†’ flopDem âŠ™ Ï† âŠ™ flipDem) recto-verso) âŸ©
            (flopDem âŠ™ flipDem) x                       â‰¡âŸ¨âŸ©
            x âˆ)
          }

        h : Equivalence.Isomorphism (idToIso A B)
        h = ff , invv
        univalent : isEquiv (A â‰¡ B) (A â‰… B)
          (Univalence.idToIso (swap â„‚.isIdentity) A B)
        univalent = fromIso _ _ h

      isCategory : IsCategory opRaw
      IsCategory.isPreCategory isCategory = isPreCategory
      IsCategory.univalent     isCategory = univalent

    opposite : Category â„“a â„“b
    Category.raw        opposite = opRaw
    Category.isCategory opposite = isCategory

  -- As demonstrated here a side-effect of having no-eta-equality on constructors
  -- means that we need to pick things apart to show that things are indeed
  -- definitionally equal. I.e; a thing that would normally be provable in one
  -- line now takes 13!! Admittedly it's a simple proof.
  module _ {â„‚ : Category â„“a â„“b} where
    open Category â„‚
    private
      -- Since they really are definitionally equal we just need to pick apart
      -- the data-type.
      rawInv : Category.raw (opposite (opposite â„‚)) â‰¡ raw
      RawCategory.Object   (rawInv _) = Object
      RawCategory.Arrow    (rawInv _) = Arrow
      RawCategory.identity (rawInv _) = identity
      RawCategory._âˆ˜_      (rawInv _) = _âˆ˜_

    oppositeIsInvolution : opposite (opposite â„‚) â‰¡ â„‚
    oppositeIsInvolution = Categoryâ‰¡ rawInv

open Opposite public
