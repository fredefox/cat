<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Core.Primitives</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-

This file document and export the main primitives of Cubical Agda. It
also defines some basic derived operations (composition and filling).

-}</a>
<a id="149" class="Symbol">{-#</a> <a id="153" class="Keyword">OPTIONS</a> <a id="161" class="Pragma">--cubical</a> <a id="171" class="Pragma">--safe</a> <a id="178" class="Symbol">#-}</a>
<a id="182" class="Keyword">module</a> <a id="189" href="Cubical.Core.Primitives.html" class="Module">Cubical.Core.Primitives</a> <a id="213" class="Keyword">where</a>

<a id="220" class="Keyword">open</a> <a id="225" class="Keyword">import</a> <a id="232" href="Agda.Builtin.Cubical.Path.html" class="Module">Agda.Builtin.Cubical.Path</a> <a id="258" class="Keyword">public</a>
<a id="265" class="Keyword">open</a> <a id="270" class="Keyword">import</a> <a id="277" href="Agda.Builtin.Cubical.Sub.html" class="Module">Agda.Builtin.Cubical.Sub</a> <a id="302" class="Keyword">public</a>
  <a id="311" class="Keyword">renaming</a> <a id="320" class="Symbol">(</a> <a id="322" href="Agda.Builtin.Cubical.Sub.html#216" class="Postulate">inc</a> <a id="326" class="Symbol">to</a> <a id="Sub.inc"></a><a id="329" href="Cubical.Core.Primitives.html#329" class="Postulate">inS</a>
           <a id="344" class="Symbol">;</a> <a id="346" href="Agda.Builtin.Cubical.Sub.html#318" class="Primitive">primSubOut</a> <a id="357" class="Symbol">to</a> <a id="Sub.primSubOut"></a><a id="360" href="Cubical.Core.Primitives.html#360" class="Primitive">outS</a>
           <a id="376" class="Symbol">)</a>
<a id="378" class="Keyword">open</a> <a id="383" class="Keyword">import</a> <a id="390" href="Agda.Primitive.Cubical.html" class="Module">Agda.Primitive.Cubical</a> <a id="413" class="Keyword">public</a>
  <a id="422" class="Keyword">renaming</a> <a id="431" class="Symbol">(</a> <a id="433" href="Agda.Primitive.Cubical.html#241" class="Primitive">primIMin</a>       <a id="448" class="Symbol">to</a> <a id="primIMin"></a><a id="451" href="Cubical.Core.Primitives.html#451" class="Primitive">_∧_</a>  <a id="456" class="Comment">-- I → I → I</a>
           <a id="480" class="Symbol">;</a> <a id="482" href="Agda.Primitive.Cubical.html#266" class="Primitive">primIMax</a>       <a id="497" class="Symbol">to</a> <a id="primIMax"></a><a id="500" href="Cubical.Core.Primitives.html#500" class="Primitive">_∨_</a>  <a id="505" class="Comment">-- I → I → I</a>
           <a id="529" class="Symbol">;</a> <a id="531" href="Agda.Primitive.Cubical.html#291" class="Primitive">primINeg</a>       <a id="546" class="Symbol">to</a> <a id="primINeg"></a><a id="549" href="Cubical.Core.Primitives.html#549" class="Primitive">~_</a>   <a id="554" class="Comment">-- I → I</a>
           <a id="574" class="Symbol">;</a> <a id="576" href="Agda.Primitive.Cubical.html#759" class="Postulate">isOneEmpty</a>     <a id="591" class="Symbol">to</a> <a id="isOneEmpty"></a><a id="594" href="Cubical.Core.Primitives.html#594" class="Postulate">empty</a>
           <a id="611" class="Symbol">;</a> <a id="613" href="Agda.Primitive.Cubical.html#1140" class="Primitive">primComp</a>       <a id="628" class="Symbol">to</a> <a id="primComp"></a><a id="631" href="Cubical.Core.Primitives.html#631" class="Primitive">comp</a>
           <a id="647" class="Symbol">;</a> <a id="649" href="Agda.Primitive.Cubical.html#1367" class="Primitive">primHComp</a>      <a id="664" class="Symbol">to</a> <a id="primHComp"></a><a id="667" href="Cubical.Core.Primitives.html#667" class="Primitive">hcomp</a>
           <a id="684" class="Symbol">;</a> <a id="686" href="Agda.Primitive.Cubical.html#1294" class="Primitive">primTransp</a>     <a id="701" class="Symbol">to</a> <a id="primTransp"></a><a id="704" href="Cubical.Core.Primitives.html#704" class="Primitive">transp</a>
           <a id="722" class="Symbol">;</a> <a id="724" href="Agda.Primitive.Cubical.html#377" class="Postulate">itIsOne</a>        <a id="739" class="Symbol">to</a> <a id="itIsOne"></a><a id="742" href="Cubical.Core.Primitives.html#742" class="Postulate">1=1</a> <a id="746" class="Symbol">)</a>

<a id="749" class="Comment">-- These two are to make sure all the primitives are loaded and ready</a>
<a id="819" class="Comment">-- to compute hcomp/transp for the universe.</a>
<a id="864" class="Keyword">import</a> <a id="871" href="Agda.Builtin.Cubical.Glue.html" class="Module">Agda.Builtin.Cubical.Glue</a>
<a id="897" class="Comment">-- HCompU is already imported from Glue, and older Agda versions do</a>
<a id="965" class="Comment">-- not have it. So we comment it out for now.</a>
<a id="1011" class="Comment">-- import Agda.Builtin.Cubical.HCompU</a>

<a id="1050" class="Keyword">open</a> <a id="1055" class="Keyword">import</a> <a id="1062" href="Agda.Primitive.html" class="Module">Agda.Primitive</a> <a id="1077" class="Keyword">public</a>
  <a id="1086" class="Keyword">using</a>    <a id="1095" class="Symbol">(</a> <a id="1097" href="Agda.Primitive.html#423" class="Postulate">Level</a> <a id="1103" class="Symbol">)</a>
  <a id="1107" class="Keyword">renaming</a> <a id="1116" class="Symbol">(</a> <a id="1118" href="Agda.Primitive.html#590" class="Primitive">lzero</a> <a id="1124" class="Symbol">to</a> <a id="lzero"></a><a id="1127" href="Cubical.Core.Primitives.html#1127" class="Primitive">ℓ-zero</a>
           <a id="1145" class="Symbol">;</a> <a id="1147" href="Agda.Primitive.html#606" class="Primitive">lsuc</a>  <a id="1153" class="Symbol">to</a> <a id="lsuc"></a><a id="1156" href="Cubical.Core.Primitives.html#1156" class="Primitive">ℓ-suc</a>
           <a id="1173" class="Symbol">;</a> <a id="1175" href="Agda.Primitive.html#636" class="Primitive Operator">_⊔_</a>   <a id="1181" class="Symbol">to</a> <a id="_⊔_"></a><a id="1184" href="Cubical.Core.Primitives.html#1184" class="Primitive Operator">ℓ-max</a>
           <a id="1201" class="Symbol">;</a> <a id="1203" href="Agda.Primitive.html#787" class="Primitive">Setω</a>  <a id="1209" class="Symbol">to</a> <a id="Setω"></a><a id="1212" href="Cubical.Core.Primitives.html#1212" class="Primitive">Typeω</a> <a id="1218" class="Symbol">)</a>
<a id="1220" class="Keyword">open</a> <a id="1225" class="Keyword">import</a> <a id="1232" href="Agda.Builtin.Sigma.html" class="Module">Agda.Builtin.Sigma</a> <a id="1251" class="Keyword">public</a>

<a id="Type"></a><a id="1259" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="1264" class="Symbol">:</a> <a id="1266" class="Symbol">(</a><a id="1267" href="Cubical.Core.Primitives.html#1267" class="Bound">ℓ</a> <a id="1269" class="Symbol">:</a> <a id="1271" href="Agda.Primitive.html#423" class="Postulate">Level</a><a id="1276" class="Symbol">)</a> <a id="1278" class="Symbol">→</a> <a id="1280" class="PrimitiveType">Set</a> <a id="1284" class="Symbol">(</a><a id="1285" href="Cubical.Core.Primitives.html#1156" class="Primitive">ℓ-suc</a> <a id="1291" href="Cubical.Core.Primitives.html#1267" class="Bound">ℓ</a><a id="1292" class="Symbol">)</a>
<a id="1294" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="1299" href="Cubical.Core.Primitives.html#1299" class="Bound">ℓ</a> <a id="1301" class="Symbol">=</a> <a id="1303" class="PrimitiveType">Set</a> <a id="1307" href="Cubical.Core.Primitives.html#1299" class="Bound">ℓ</a>

<a id="Type₀"></a><a id="1310" href="Cubical.Core.Primitives.html#1310" class="Function">Type₀</a> <a id="1316" class="Symbol">:</a> <a id="1318" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="1323" class="Symbol">(</a><a id="1324" href="Cubical.Core.Primitives.html#1156" class="Primitive">ℓ-suc</a> <a id="1330" href="Cubical.Core.Primitives.html#1127" class="Primitive">ℓ-zero</a><a id="1336" class="Symbol">)</a>
<a id="1338" href="Cubical.Core.Primitives.html#1310" class="Function">Type₀</a> <a id="1344" class="Symbol">=</a> <a id="1346" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="1351" href="Cubical.Core.Primitives.html#1127" class="Primitive">ℓ-zero</a>

<a id="Type₁"></a><a id="1359" href="Cubical.Core.Primitives.html#1359" class="Function">Type₁</a> <a id="1365" class="Symbol">:</a> <a id="1367" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="1372" class="Symbol">(</a><a id="1373" href="Cubical.Core.Primitives.html#1156" class="Primitive">ℓ-suc</a> <a id="1379" class="Symbol">(</a><a id="1380" href="Cubical.Core.Primitives.html#1156" class="Primitive">ℓ-suc</a> <a id="1386" href="Cubical.Core.Primitives.html#1127" class="Primitive">ℓ-zero</a><a id="1392" class="Symbol">))</a>
<a id="1395" href="Cubical.Core.Primitives.html#1359" class="Function">Type₁</a> <a id="1401" class="Symbol">=</a> <a id="1403" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="1408" class="Symbol">(</a><a id="1409" href="Cubical.Core.Primitives.html#1156" class="Primitive">ℓ-suc</a> <a id="1415" href="Cubical.Core.Primitives.html#1127" class="Primitive">ℓ-zero</a><a id="1421" class="Symbol">)</a>

<a id="1424" class="Comment">-- This file document the Cubical Agda primitives. The primitives</a>
<a id="1490" class="Comment">-- themselves are bound by the Agda files imported above.</a>

<a id="1549" class="Comment">-- * The Interval</a>
<a id="1567" class="Comment">-- I : Typeω</a>

<a id="1581" class="Comment">-- Endpoints, Connections, Reversal</a>
<a id="1617" class="Comment">-- i0 i1   : I</a>
<a id="1632" class="Comment">-- _∧_ _∨_ : I → I → I</a>
<a id="1655" class="Comment">-- ~_      : I → I</a>


<a id="1676" class="Comment">-- * Dependent path type. (Path over Path)</a>

<a id="1720" class="Comment">-- Introduced with lambda abstraction and eliminated with application,</a>
<a id="1791" class="Comment">-- just like function types.</a>

<a id="1821" class="Comment">-- PathP : ∀ {ℓ} (A : I → Type ℓ) → A i0 → A i1 → Type ℓ</a>

<a id="1879" class="Keyword">infix</a> <a id="1885" class="Number">4</a> <a id="1887" href="Cubical.Core.Primitives.html#1895" class="Function Operator">_[_≡_]</a>

<a id="_[_≡_]"></a><a id="1895" href="Cubical.Core.Primitives.html#1895" class="Function Operator">_[_≡_]</a> <a id="1902" class="Symbol">:</a> <a id="1904" class="Symbol">∀</a> <a id="1906" class="Symbol">{</a><a id="1907" href="Cubical.Core.Primitives.html#1907" class="Bound">ℓ</a><a id="1908" class="Symbol">}</a> <a id="1910" class="Symbol">(</a><a id="1911" href="Cubical.Core.Primitives.html#1911" class="Bound">A</a> <a id="1913" class="Symbol">:</a> <a id="1915" href="Agda.Primitive.Cubical.html#101" class="Datatype">I</a> <a id="1917" class="Symbol">→</a> <a id="1919" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="1924" href="Cubical.Core.Primitives.html#1907" class="Bound">ℓ</a><a id="1925" class="Symbol">)</a> <a id="1927" class="Symbol">→</a> <a id="1929" href="Cubical.Core.Primitives.html#1911" class="Bound">A</a> <a id="1931" href="Agda.Primitive.Cubical.html#143" class="InductiveConstructor">i0</a> <a id="1934" class="Symbol">→</a> <a id="1936" href="Cubical.Core.Primitives.html#1911" class="Bound">A</a> <a id="1938" href="Agda.Primitive.Cubical.html#171" class="InductiveConstructor">i1</a> <a id="1941" class="Symbol">→</a> <a id="1943" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="1948" href="Cubical.Core.Primitives.html#1907" class="Bound">ℓ</a>
<a id="1950" href="Cubical.Core.Primitives.html#1895" class="Function Operator">_[_≡_]</a> <a id="1957" class="Symbol">=</a> <a id="1959" href="Agda.Builtin.Cubical.Path.html#190" class="Postulate">PathP</a>


<a id="1967" class="Comment">-- Non dependent path types</a>

<a id="Path"></a><a id="1996" href="Cubical.Core.Primitives.html#1996" class="Function">Path</a> <a id="2001" class="Symbol">:</a> <a id="2003" class="Symbol">∀</a> <a id="2005" class="Symbol">{</a><a id="2006" href="Cubical.Core.Primitives.html#2006" class="Bound">ℓ</a><a id="2007" class="Symbol">}</a> <a id="2009" class="Symbol">(</a><a id="2010" href="Cubical.Core.Primitives.html#2010" class="Bound">A</a> <a id="2012" class="Symbol">:</a> <a id="2014" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="2019" href="Cubical.Core.Primitives.html#2006" class="Bound">ℓ</a><a id="2020" class="Symbol">)</a> <a id="2022" class="Symbol">→</a> <a id="2024" href="Cubical.Core.Primitives.html#2010" class="Bound">A</a> <a id="2026" class="Symbol">→</a> <a id="2028" href="Cubical.Core.Primitives.html#2010" class="Bound">A</a> <a id="2030" class="Symbol">→</a> <a id="2032" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="2037" href="Cubical.Core.Primitives.html#2006" class="Bound">ℓ</a>
<a id="2039" href="Cubical.Core.Primitives.html#1996" class="Function">Path</a> <a id="2044" href="Cubical.Core.Primitives.html#2044" class="Bound">A</a> <a id="2046" href="Cubical.Core.Primitives.html#2046" class="Bound">a</a> <a id="2048" href="Cubical.Core.Primitives.html#2048" class="Bound">b</a> <a id="2050" class="Symbol">=</a> <a id="2052" href="Agda.Builtin.Cubical.Path.html#190" class="Postulate">PathP</a> <a id="2058" class="Symbol">(λ</a> <a id="2061" href="Cubical.Core.Primitives.html#2061" class="Bound">_</a> <a id="2063" class="Symbol">→</a> <a id="2065" href="Cubical.Core.Primitives.html#2044" class="Bound">A</a><a id="2066" class="Symbol">)</a> <a id="2068" href="Cubical.Core.Primitives.html#2046" class="Bound">a</a> <a id="2070" href="Cubical.Core.Primitives.html#2048" class="Bound">b</a>

<a id="2073" class="Comment">-- PathP (λ i → A) x y gets printed as x ≡ y when A does not mention i.</a>
<a id="2145" class="Comment">--  _≡_ : ∀ {ℓ} {A : Type ℓ} → A → A → Type ℓ</a>
<a id="2191" class="Comment">--  _≡_ {A = A} = PathP (λ _ → A)</a>


<a id="2227" class="Comment">-- * @IsOne r@ represents the constraint &quot;r = i1&quot;.</a>
<a id="2278" class="Comment">-- Often we will use &quot;φ&quot; for elements of I, when we intend to use them</a>
<a id="2349" class="Comment">-- with IsOne (or Partial[P]).</a>
<a id="2380" class="Comment">-- IsOne : I → Typeω</a>

<a id="2402" class="Comment">-- i1 is indeed equal to i1.</a>
<a id="2431" class="Comment">-- 1=1 : IsOne i1</a>


<a id="2451" class="Comment">-- * Types of partial elements, and their dependent version.</a>

<a id="2513" class="Comment">-- &quot;Partial φ A&quot; is a special version of &quot;IsOne φ → A&quot; with a more</a>
<a id="2580" class="Comment">-- extensional judgmental equality.</a>
<a id="2616" class="Comment">-- &quot;PartialP φ A&quot; allows &quot;A&quot; to be defined only on &quot;φ&quot;.</a>

<a id="2673" class="Comment">-- Partial : ∀ {ℓ} → I → Type ℓ → Typeω</a>
<a id="2713" class="Comment">-- PartialP : ∀ {ℓ} → (φ : I) → Partial φ (Type ℓ) → Typeω</a>

<a id="2773" class="Comment">-- Partial elements are introduced by pattern matching with (r = i0)</a>
<a id="2842" class="Comment">-- or (r = i1) constraints, like so:</a>

<a id="2880" class="Keyword">private</a>
  <a id="sys"></a><a id="2890" href="Cubical.Core.Primitives.html#2890" class="Function">sys</a> <a id="2894" class="Symbol">:</a> <a id="2896" class="Symbol">∀</a> <a id="2898" href="Cubical.Core.Primitives.html#2898" class="Bound">i</a> <a id="2900" class="Symbol">→</a> <a id="2902" href="Agda.Primitive.Cubical.html#699" class="Primitive">Partial</a> <a id="2910" class="Symbol">(</a><a id="2911" href="Cubical.Core.Primitives.html#2898" class="Bound">i</a> <a id="2913" href="Cubical.Core.Primitives.html#500" class="Primitive Operator">∨</a> <a id="2915" href="Cubical.Core.Primitives.html#549" class="Primitive Operator">~</a> <a id="2917" href="Cubical.Core.Primitives.html#2898" class="Bound">i</a><a id="2918" class="Symbol">)</a> <a id="2920" href="Cubical.Core.Primitives.html#1359" class="Function">Type₁</a>
  <a id="2928" href="Cubical.Core.Primitives.html#2890" class="Function">sys</a> <a id="2932" href="Cubical.Core.Primitives.html#2932" class="Bound">i</a> <a id="2934" class="Symbol">(</a><a id="2935" href="Cubical.Core.Primitives.html#2932" class="Bound">i</a> <a id="2937" class="Symbol">=</a> <a id="2939" href="Agda.Primitive.Cubical.html#143" class="InductiveConstructor">i0</a><a id="2941" class="Symbol">)</a> <a id="2943" class="Symbol">=</a> <a id="2945" href="Cubical.Core.Primitives.html#1310" class="Function">Type₀</a>
  <a id="2953" href="Cubical.Core.Primitives.html#2890" class="Function">sys</a> <a id="2957" href="Cubical.Core.Primitives.html#2957" class="Bound">i</a> <a id="2959" class="Symbol">(</a><a id="2960" href="Cubical.Core.Primitives.html#2957" class="Bound">i</a> <a id="2962" class="Symbol">=</a> <a id="2964" href="Agda.Primitive.Cubical.html#171" class="InductiveConstructor">i1</a><a id="2966" class="Symbol">)</a> <a id="2968" class="Symbol">=</a> <a id="2970" href="Cubical.Core.Primitives.html#1310" class="Function">Type₀</a> <a id="2976" class="Symbol">→</a> <a id="2978" href="Cubical.Core.Primitives.html#1310" class="Function">Type₀</a>

  <a id="2987" class="Comment">-- It also works with pattern matching lambdas:</a>
  <a id="3037" class="Comment">--  http://wiki.portal.chalmers.se/agda/pmwiki.php?n=ReferenceManual.PatternMatchingLambdas</a>
  <a id="sys&#39;"></a><a id="3131" href="Cubical.Core.Primitives.html#3131" class="Function">sys&#39;</a> <a id="3136" class="Symbol">:</a> <a id="3138" class="Symbol">∀</a> <a id="3140" href="Cubical.Core.Primitives.html#3140" class="Bound">i</a> <a id="3142" class="Symbol">→</a> <a id="3144" href="Agda.Primitive.Cubical.html#699" class="Primitive">Partial</a> <a id="3152" class="Symbol">(</a><a id="3153" href="Cubical.Core.Primitives.html#3140" class="Bound">i</a> <a id="3155" href="Cubical.Core.Primitives.html#500" class="Primitive Operator">∨</a> <a id="3157" href="Cubical.Core.Primitives.html#549" class="Primitive Operator">~</a> <a id="3159" href="Cubical.Core.Primitives.html#3140" class="Bound">i</a><a id="3160" class="Symbol">)</a> <a id="3162" href="Cubical.Core.Primitives.html#1359" class="Function">Type₁</a>
  <a id="3170" href="Cubical.Core.Primitives.html#3131" class="Function">sys&#39;</a> <a id="3175" href="Cubical.Core.Primitives.html#3175" class="Bound">i</a> <a id="3177" class="Symbol">=</a> <a id="3179" class="Symbol">λ</a> <a id="3181" class="Symbol">{</a> <a id="3183" class="Symbol">(</a><a id="3184" href="Cubical.Core.Primitives.html#3175" class="Bound">i</a> <a id="3186" class="Symbol">=</a> <a id="3188" href="Agda.Primitive.Cubical.html#143" class="InductiveConstructor">i0</a><a id="3190" class="Symbol">)</a> <a id="3192" class="Symbol">→</a> <a id="3194" href="Cubical.Core.Primitives.html#1310" class="Function">Type₀</a>
             <a id="3213" class="Symbol">;</a> <a id="3215" class="Symbol">(</a><a id="3216" href="Cubical.Core.Primitives.html#3175" class="Bound">i</a> <a id="3218" class="Symbol">=</a> <a id="3220" href="Agda.Primitive.Cubical.html#171" class="InductiveConstructor">i1</a><a id="3222" class="Symbol">)</a> <a id="3224" class="Symbol">→</a> <a id="3226" href="Cubical.Core.Primitives.html#1310" class="Function">Type₀</a> <a id="3232" class="Symbol">→</a> <a id="3234" href="Cubical.Core.Primitives.html#1310" class="Function">Type₀</a>
             <a id="3253" class="Symbol">}</a>

  <a id="3258" class="Comment">-- When the cases overlap they must agree.</a>
  <a id="sys2"></a><a id="3303" href="Cubical.Core.Primitives.html#3303" class="Function">sys2</a> <a id="3308" class="Symbol">:</a> <a id="3310" class="Symbol">∀</a> <a id="3312" href="Cubical.Core.Primitives.html#3312" class="Bound">i</a> <a id="3314" href="Cubical.Core.Primitives.html#3314" class="Bound">j</a> <a id="3316" class="Symbol">→</a> <a id="3318" href="Agda.Primitive.Cubical.html#699" class="Primitive">Partial</a> <a id="3326" class="Symbol">(</a><a id="3327" href="Cubical.Core.Primitives.html#3312" class="Bound">i</a> <a id="3329" href="Cubical.Core.Primitives.html#500" class="Primitive Operator">∨</a> <a id="3331" class="Symbol">(</a><a id="3332" href="Cubical.Core.Primitives.html#3312" class="Bound">i</a> <a id="3334" href="Cubical.Core.Primitives.html#451" class="Primitive Operator">∧</a> <a id="3336" href="Cubical.Core.Primitives.html#3314" class="Bound">j</a><a id="3337" class="Symbol">))</a> <a id="3340" href="Cubical.Core.Primitives.html#1359" class="Function">Type₁</a>
  <a id="3348" href="Cubical.Core.Primitives.html#3303" class="Function">sys2</a> <a id="3353" href="Cubical.Core.Primitives.html#3353" class="Bound">i</a> <a id="3355" href="Cubical.Core.Primitives.html#3355" class="Bound">j</a> <a id="3357" class="Symbol">=</a> <a id="3359" class="Symbol">λ</a> <a id="3361" class="Symbol">{</a> <a id="3363" class="Symbol">(</a><a id="3364" href="Cubical.Core.Primitives.html#3353" class="Bound">i</a> <a id="3366" class="Symbol">=</a> <a id="3368" href="Agda.Primitive.Cubical.html#171" class="InductiveConstructor">i1</a><a id="3370" class="Symbol">)</a>          <a id="3381" class="Symbol">→</a> <a id="3383" href="Cubical.Core.Primitives.html#1310" class="Function">Type₀</a>
               <a id="3404" class="Symbol">;</a> <a id="3406" class="Symbol">(</a><a id="3407" href="Cubical.Core.Primitives.html#3353" class="Bound">i</a> <a id="3409" class="Symbol">=</a> <a id="3411" href="Agda.Primitive.Cubical.html#171" class="InductiveConstructor">i1</a><a id="3413" class="Symbol">)</a> <a id="3415" class="Symbol">(</a><a id="3416" href="Cubical.Core.Primitives.html#3355" class="Bound">j</a> <a id="3418" class="Symbol">=</a> <a id="3420" href="Agda.Primitive.Cubical.html#171" class="InductiveConstructor">i1</a><a id="3422" class="Symbol">)</a> <a id="3424" class="Symbol">→</a> <a id="3426" href="Cubical.Core.Primitives.html#1310" class="Function">Type₀</a>
               <a id="3447" class="Symbol">}</a>

  <a id="3452" class="Comment">-- (i0 = i1) is actually absurd.</a>
  <a id="sys3"></a><a id="3487" href="Cubical.Core.Primitives.html#3487" class="Function">sys3</a> <a id="3492" class="Symbol">:</a> <a id="3494" href="Agda.Primitive.Cubical.html#699" class="Primitive">Partial</a> <a id="3502" href="Agda.Primitive.Cubical.html#143" class="InductiveConstructor">i0</a> <a id="3505" href="Cubical.Core.Primitives.html#1359" class="Function">Type₁</a>
  <a id="3513" href="Cubical.Core.Primitives.html#3487" class="Function">sys3</a> <a id="3518" class="Symbol">=</a> <a id="3520" class="Symbol">λ</a> <a id="3522" class="Symbol">{</a> <a id="3524" class="Symbol">()</a> <a id="3527" class="Symbol">}</a>


<a id="3531" class="Comment">-- * There are cubical subtypes as in CCHM. Note that these are not</a>
<a id="3599" class="Comment">-- fibrant (hence in Typeω):</a>

<a id="_[_↦_]"></a><a id="3629" href="Cubical.Core.Primitives.html#3629" class="Function Operator">_[_↦_]</a> <a id="3636" class="Symbol">:</a> <a id="3638" class="Symbol">∀</a> <a id="3640" class="Symbol">{</a><a id="3641" href="Cubical.Core.Primitives.html#3641" class="Bound">ℓ</a><a id="3642" class="Symbol">}</a> <a id="3644" class="Symbol">(</a><a id="3645" href="Cubical.Core.Primitives.html#3645" class="Bound">A</a> <a id="3647" class="Symbol">:</a> <a id="3649" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="3654" href="Cubical.Core.Primitives.html#3641" class="Bound">ℓ</a><a id="3655" class="Symbol">)</a> <a id="3657" class="Symbol">(</a><a id="3658" href="Cubical.Core.Primitives.html#3658" class="Bound">φ</a> <a id="3660" class="Symbol">:</a> <a id="3662" href="Agda.Primitive.Cubical.html#101" class="Datatype">I</a><a id="3663" class="Symbol">)</a> <a id="3665" class="Symbol">(</a><a id="3666" href="Cubical.Core.Primitives.html#3666" class="Bound">u</a> <a id="3668" class="Symbol">:</a> <a id="3670" href="Agda.Primitive.Cubical.html#699" class="Primitive">Partial</a> <a id="3678" href="Cubical.Core.Primitives.html#3658" class="Bound">φ</a> <a id="3680" href="Cubical.Core.Primitives.html#3645" class="Bound">A</a><a id="3681" class="Symbol">)</a> <a id="3683" class="Symbol">→</a> <a id="3685" href="Cubical.Core.Primitives.html#1212" class="Primitive">Typeω</a>
<a id="3691" href="Cubical.Core.Primitives.html#3691" class="Bound">A</a> <a id="3693" href="Cubical.Core.Primitives.html#3629" class="Function Operator">[</a> <a id="3695" href="Cubical.Core.Primitives.html#3695" class="Bound">φ</a> <a id="3697" href="Cubical.Core.Primitives.html#3629" class="Function Operator">↦</a> <a id="3699" href="Cubical.Core.Primitives.html#3699" class="Bound">u</a> <a id="3701" href="Cubical.Core.Primitives.html#3629" class="Function Operator">]</a> <a id="3703" class="Symbol">=</a> <a id="3705" href="Agda.Builtin.Cubical.Sub.html#191" class="Postulate">Sub</a> <a id="3709" href="Cubical.Core.Primitives.html#3691" class="Bound">A</a> <a id="3711" href="Cubical.Core.Primitives.html#3695" class="Bound">φ</a> <a id="3713" href="Cubical.Core.Primitives.html#3699" class="Bound">u</a>

<a id="3716" class="Keyword">infix</a> <a id="3722" class="Number">4</a> <a id="3724" href="Cubical.Core.Primitives.html#3629" class="Function Operator">_[_↦_]</a>

<a id="3732" class="Comment">-- Any element u : A can be seen as an element of A [ φ ↦ u ] which</a>
<a id="3800" class="Comment">-- agrees with u on φ:</a>

<a id="3824" class="Comment">-- inS : ∀ {ℓ} {A : Type ℓ} {φ} (u : A) → A [ φ ↦ (λ _ → u) ]</a>

<a id="3887" class="Comment">-- One can also forget that an element agrees with u on φ:</a>

<a id="3947" class="Comment">-- outS : ∀ {ℓ} {A : Type ℓ} {φ : I} {u : Partial φ A} → A [ φ ↦ u ] → A</a>


<a id="4022" class="Comment">-- * Composition operation according to [CCHM 18].</a>
<a id="4073" class="Comment">-- When calling &quot;comp A φ u a&quot; Agda makes sure that &quot;a&quot; agrees with &quot;u i0&quot; on &quot;φ&quot;.</a>
<a id="4156" class="Comment">-- compCCHM : ∀ {ℓ} (A : (i : I) → Type ℓ) (φ : I) (u : ∀ i → Partial φ (A i)) (a : A i0) → A i1</a>

<a id="4254" class="Comment">-- Note: this is not recommended to use, instead use the CHM</a>
<a id="4315" class="Comment">-- primitives! The reason is that these work with HITs and produce</a>
<a id="4382" class="Comment">-- fewer empty systems.</a>


<a id="4408" class="Comment">-- * Generalized transport and homogeneous composition [CHM 18].</a>

<a id="4474" class="Comment">-- When calling &quot;transp A φ a&quot; Agda makes sure that &quot;A&quot; is constant on &quot;φ&quot; (see below).</a>
<a id="4562" class="Comment">-- transp : ∀ {ℓ} (A : I → Type ℓ) (φ : I) (a : A i0) → A i1</a>

<a id="4624" class="Comment">-- &quot;A&quot; being constant on &quot;φ&quot; means that &quot;A&quot; should be a constant function whenever the</a>
<a id="4711" class="Comment">-- constraint &quot;φ = i1&quot; is satisfied. For example:</a>
<a id="4761" class="Comment">-- - If &quot;φ&quot; is &quot;i0&quot; then &quot;A&quot; can be anything, since this condition is vacuously true.</a>
<a id="4847" class="Comment">-- - If &quot;φ&quot; is &quot;i1&quot; then &quot;A&quot; must be a constant function.</a>
<a id="4905" class="Comment">-- - If &quot;φ&quot; is some in-scope variable &quot;i&quot; then &quot;A&quot; only needs to be a constant function</a>
<a id="4993" class="Comment">--   when substituting &quot;i1&quot; for &quot;i&quot;.</a>

<a id="5031" class="Comment">-- When calling &quot;hcomp A φ u a&quot; Agda makes sure that &quot;a&quot; agrees with &quot;u i0&quot; on &quot;φ&quot;.</a>
<a id="5115" class="Comment">-- hcomp : ∀ {ℓ} {A : Type ℓ} {φ : I} (u : I → Partial φ A) (a : A) → A</a>

<a id="5188" class="Keyword">private</a>
  <a id="5198" class="Keyword">variable</a>
    <a id="5211" href="Cubical.Core.Primitives.html#5211" class="Generalizable">ℓ</a>  <a id="5214" class="Symbol">:</a> <a id="5216" href="Agda.Primitive.html#423" class="Postulate">Level</a>
    <a id="5226" href="Cubical.Core.Primitives.html#5226" class="Generalizable">ℓ′</a> <a id="5229" class="Symbol">:</a> <a id="5231" href="Agda.Primitive.Cubical.html#101" class="Datatype">I</a> <a id="5233" class="Symbol">→</a> <a id="5235" href="Agda.Primitive.html#423" class="Postulate">Level</a>

<a id="5242" class="Comment">-- Homogeneous filling</a>
<a id="hfill"></a><a id="5265" href="Cubical.Core.Primitives.html#5265" class="Function">hfill</a> <a id="5271" class="Symbol">:</a> <a id="5273" class="Symbol">{</a><a id="5274" href="Cubical.Core.Primitives.html#5274" class="Bound">A</a> <a id="5276" class="Symbol">:</a> <a id="5278" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="5283" href="Cubical.Core.Primitives.html#5211" class="Generalizable">ℓ</a><a id="5284" class="Symbol">}</a>
        <a id="5294" class="Symbol">{</a><a id="5295" href="Cubical.Core.Primitives.html#5295" class="Bound">φ</a> <a id="5297" class="Symbol">:</a> <a id="5299" href="Agda.Primitive.Cubical.html#101" class="Datatype">I</a><a id="5300" class="Symbol">}</a>
        <a id="5310" class="Symbol">(</a><a id="5311" href="Cubical.Core.Primitives.html#5311" class="Bound">u</a> <a id="5313" class="Symbol">:</a> <a id="5315" class="Symbol">∀</a> <a id="5317" href="Cubical.Core.Primitives.html#5317" class="Bound">i</a> <a id="5319" class="Symbol">→</a> <a id="5321" href="Agda.Primitive.Cubical.html#699" class="Primitive">Partial</a> <a id="5329" href="Cubical.Core.Primitives.html#5295" class="Bound">φ</a> <a id="5331" href="Cubical.Core.Primitives.html#5274" class="Bound">A</a><a id="5332" class="Symbol">)</a>
        <a id="5342" class="Symbol">(</a><a id="5343" href="Cubical.Core.Primitives.html#5343" class="Bound">u0</a> <a id="5346" class="Symbol">:</a> <a id="5348" href="Cubical.Core.Primitives.html#5274" class="Bound">A</a> <a id="5350" href="Cubical.Core.Primitives.html#3629" class="Function Operator">[</a> <a id="5352" href="Cubical.Core.Primitives.html#5295" class="Bound">φ</a> <a id="5354" href="Cubical.Core.Primitives.html#3629" class="Function Operator">↦</a> <a id="5356" href="Cubical.Core.Primitives.html#5311" class="Bound">u</a> <a id="5358" href="Agda.Primitive.Cubical.html#143" class="InductiveConstructor">i0</a> <a id="5361" href="Cubical.Core.Primitives.html#3629" class="Function Operator">]</a><a id="5362" class="Symbol">)</a>
        <a id="5372" class="Comment">-----------------------</a>
        <a id="5404" class="Symbol">(</a><a id="5405" href="Cubical.Core.Primitives.html#5405" class="Bound">i</a> <a id="5407" class="Symbol">:</a> <a id="5409" href="Agda.Primitive.Cubical.html#101" class="Datatype">I</a><a id="5410" class="Symbol">)</a> <a id="5412" class="Symbol">→</a> <a id="5414" href="Cubical.Core.Primitives.html#5274" class="Bound">A</a>
<a id="5416" href="Cubical.Core.Primitives.html#5265" class="Function">hfill</a> <a id="5422" class="Symbol">{</a><a id="5423" class="Argument">φ</a> <a id="5425" class="Symbol">=</a> <a id="5427" href="Cubical.Core.Primitives.html#5427" class="Bound">φ</a><a id="5428" class="Symbol">}</a> <a id="5430" href="Cubical.Core.Primitives.html#5430" class="Bound">u</a> <a id="5432" href="Cubical.Core.Primitives.html#5432" class="Bound">u0</a> <a id="5435" href="Cubical.Core.Primitives.html#5435" class="Bound">i</a> <a id="5437" class="Symbol">=</a>
  <a id="5441" href="Cubical.Core.Primitives.html#667" class="Primitive">hcomp</a> <a id="5447" class="Symbol">(λ</a> <a id="5450" href="Cubical.Core.Primitives.html#5450" class="Bound">j</a> <a id="5452" class="Symbol">→</a> <a id="5454" class="Symbol">λ</a> <a id="5456" class="Symbol">{</a> <a id="5458" class="Symbol">(</a><a id="5459" href="Cubical.Core.Primitives.html#5427" class="Bound">φ</a> <a id="5461" class="Symbol">=</a> <a id="5463" href="Agda.Primitive.Cubical.html#171" class="InductiveConstructor">i1</a><a id="5465" class="Symbol">)</a> <a id="5467" class="Symbol">→</a> <a id="5469" href="Cubical.Core.Primitives.html#5430" class="Bound">u</a> <a id="5471" class="Symbol">(</a><a id="5472" href="Cubical.Core.Primitives.html#5435" class="Bound">i</a> <a id="5474" href="Cubical.Core.Primitives.html#451" class="Primitive Operator">∧</a> <a id="5476" href="Cubical.Core.Primitives.html#5450" class="Bound">j</a><a id="5477" class="Symbol">)</a> <a id="5479" href="Cubical.Core.Primitives.html#742" class="Postulate">1=1</a>
                 <a id="5500" class="Symbol">;</a> <a id="5502" class="Symbol">(</a><a id="5503" href="Cubical.Core.Primitives.html#5435" class="Bound">i</a> <a id="5505" class="Symbol">=</a> <a id="5507" href="Agda.Primitive.Cubical.html#143" class="InductiveConstructor">i0</a><a id="5509" class="Symbol">)</a> <a id="5511" class="Symbol">→</a> <a id="5513" href="Cubical.Core.Primitives.html#360" class="Primitive">outS</a> <a id="5518" href="Cubical.Core.Primitives.html#5432" class="Bound">u0</a> <a id="5521" class="Symbol">})</a>
        <a id="5532" class="Symbol">(</a><a id="5533" href="Cubical.Core.Primitives.html#360" class="Primitive">outS</a> <a id="5538" href="Cubical.Core.Primitives.html#5432" class="Bound">u0</a><a id="5540" class="Symbol">)</a>

<a id="5543" class="Comment">-- Heterogeneous composition can defined as in CHM, however we use the</a>
<a id="5614" class="Comment">-- builtin one as it doesn&#39;t require u0 to be a cubical subtype. This</a>
<a id="5684" class="Comment">-- reduces the number of inS&#39;s a lot.</a>
<a id="5722" class="Comment">-- comp : (A : ∀ i → Type (ℓ′ i))</a>
<a id="5756" class="Comment">--        {φ : I}</a>
<a id="5774" class="Comment">--        (u : ∀ i → Partial φ (A i))</a>
<a id="5812" class="Comment">--        (u0 : A i0 [ φ ↦ u i0 ])</a>
<a id="5847" class="Comment">--      → ---------------------------</a>
<a id="5885" class="Comment">--        A i1</a>
<a id="5900" class="Comment">-- comp A {φ = φ} u u0 =</a>
<a id="5925" class="Comment">--   hcomp (λ i → λ { (φ = i1) → transp (λ j → A (i ∨ j)) i (u _ 1=1) })</a>
<a id="5998" class="Comment">--         (transp A i0 (outS u0))</a>

<a id="6034" class="Comment">-- Heterogeneous filling defined using comp</a>
<a id="fill"></a><a id="6078" href="Cubical.Core.Primitives.html#6078" class="Function">fill</a> <a id="6083" class="Symbol">:</a> <a id="6085" class="Symbol">(</a><a id="6086" href="Cubical.Core.Primitives.html#6086" class="Bound">A</a> <a id="6088" class="Symbol">:</a> <a id="6090" class="Symbol">∀</a> <a id="6092" href="Cubical.Core.Primitives.html#6092" class="Bound">i</a> <a id="6094" class="Symbol">→</a> <a id="6096" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="6101" class="Symbol">(</a><a id="6102" href="Cubical.Core.Primitives.html#5226" class="Generalizable">ℓ′</a> <a id="6105" href="Cubical.Core.Primitives.html#6092" class="Bound">i</a><a id="6106" class="Symbol">))</a>
       <a id="6116" class="Symbol">{</a><a id="6117" href="Cubical.Core.Primitives.html#6117" class="Bound">φ</a> <a id="6119" class="Symbol">:</a> <a id="6121" href="Agda.Primitive.Cubical.html#101" class="Datatype">I</a><a id="6122" class="Symbol">}</a>
       <a id="6131" class="Symbol">(</a><a id="6132" href="Cubical.Core.Primitives.html#6132" class="Bound">u</a> <a id="6134" class="Symbol">:</a> <a id="6136" class="Symbol">∀</a> <a id="6138" href="Cubical.Core.Primitives.html#6138" class="Bound">i</a> <a id="6140" class="Symbol">→</a> <a id="6142" href="Agda.Primitive.Cubical.html#699" class="Primitive">Partial</a> <a id="6150" href="Cubical.Core.Primitives.html#6117" class="Bound">φ</a> <a id="6152" class="Symbol">(</a><a id="6153" href="Cubical.Core.Primitives.html#6086" class="Bound">A</a> <a id="6155" href="Cubical.Core.Primitives.html#6138" class="Bound">i</a><a id="6156" class="Symbol">))</a>
       <a id="6166" class="Symbol">(</a><a id="6167" href="Cubical.Core.Primitives.html#6167" class="Bound">u0</a> <a id="6170" class="Symbol">:</a> <a id="6172" href="Cubical.Core.Primitives.html#6086" class="Bound">A</a> <a id="6174" href="Agda.Primitive.Cubical.html#143" class="InductiveConstructor">i0</a> <a id="6177" href="Cubical.Core.Primitives.html#3629" class="Function Operator">[</a> <a id="6179" href="Cubical.Core.Primitives.html#6117" class="Bound">φ</a> <a id="6181" href="Cubical.Core.Primitives.html#3629" class="Function Operator">↦</a> <a id="6183" href="Cubical.Core.Primitives.html#6132" class="Bound">u</a> <a id="6185" href="Agda.Primitive.Cubical.html#143" class="InductiveConstructor">i0</a> <a id="6188" href="Cubical.Core.Primitives.html#3629" class="Function Operator">]</a><a id="6189" class="Symbol">)</a>
       <a id="6198" class="Comment">---------------------------</a>
       <a id="6233" class="Symbol">(</a><a id="6234" href="Cubical.Core.Primitives.html#6234" class="Bound">i</a> <a id="6236" class="Symbol">:</a> <a id="6238" href="Agda.Primitive.Cubical.html#101" class="Datatype">I</a><a id="6239" class="Symbol">)</a> <a id="6241" class="Symbol">→</a> <a id="6243" href="Cubical.Core.Primitives.html#6086" class="Bound">A</a> <a id="6245" href="Cubical.Core.Primitives.html#6234" class="Bound">i</a>
<a id="6247" href="Cubical.Core.Primitives.html#6078" class="Function">fill</a> <a id="6252" href="Cubical.Core.Primitives.html#6252" class="Bound">A</a> <a id="6254" class="Symbol">{</a><a id="6255" class="Argument">φ</a> <a id="6257" class="Symbol">=</a> <a id="6259" href="Cubical.Core.Primitives.html#6259" class="Bound">φ</a><a id="6260" class="Symbol">}</a> <a id="6262" href="Cubical.Core.Primitives.html#6262" class="Bound">u</a> <a id="6264" href="Cubical.Core.Primitives.html#6264" class="Bound">u0</a> <a id="6267" href="Cubical.Core.Primitives.html#6267" class="Bound">i</a> <a id="6269" class="Symbol">=</a>
  <a id="6273" href="Cubical.Core.Primitives.html#631" class="Primitive">comp</a> <a id="6278" class="Symbol">(λ</a> <a id="6281" href="Cubical.Core.Primitives.html#6281" class="Bound">j</a> <a id="6283" class="Symbol">→</a> <a id="6285" href="Cubical.Core.Primitives.html#6252" class="Bound">A</a> <a id="6287" class="Symbol">(</a><a id="6288" href="Cubical.Core.Primitives.html#6267" class="Bound">i</a> <a id="6290" href="Cubical.Core.Primitives.html#451" class="Primitive Operator">∧</a> <a id="6292" href="Cubical.Core.Primitives.html#6281" class="Bound">j</a><a id="6293" class="Symbol">))</a>
       <a id="6303" class="Symbol">(λ</a> <a id="6306" href="Cubical.Core.Primitives.html#6306" class="Bound">j</a> <a id="6308" class="Symbol">→</a> <a id="6310" class="Symbol">λ</a> <a id="6312" class="Symbol">{</a> <a id="6314" class="Symbol">(</a><a id="6315" href="Cubical.Core.Primitives.html#6259" class="Bound">φ</a> <a id="6317" class="Symbol">=</a> <a id="6319" href="Agda.Primitive.Cubical.html#171" class="InductiveConstructor">i1</a><a id="6321" class="Symbol">)</a> <a id="6323" class="Symbol">→</a> <a id="6325" href="Cubical.Core.Primitives.html#6262" class="Bound">u</a> <a id="6327" class="Symbol">(</a><a id="6328" href="Cubical.Core.Primitives.html#6267" class="Bound">i</a> <a id="6330" href="Cubical.Core.Primitives.html#451" class="Primitive Operator">∧</a> <a id="6332" href="Cubical.Core.Primitives.html#6306" class="Bound">j</a><a id="6333" class="Symbol">)</a> <a id="6335" href="Cubical.Core.Primitives.html#742" class="Postulate">1=1</a>
                <a id="6355" class="Symbol">;</a> <a id="6357" class="Symbol">(</a><a id="6358" href="Cubical.Core.Primitives.html#6267" class="Bound">i</a> <a id="6360" class="Symbol">=</a> <a id="6362" href="Agda.Primitive.Cubical.html#143" class="InductiveConstructor">i0</a><a id="6364" class="Symbol">)</a> <a id="6366" class="Symbol">→</a> <a id="6368" href="Cubical.Core.Primitives.html#360" class="Primitive">outS</a> <a id="6373" href="Cubical.Core.Primitives.html#6264" class="Bound">u0</a> <a id="6376" class="Symbol">})</a>
       <a id="6386" class="Symbol">(</a><a id="6387" href="Cubical.Core.Primitives.html#360" class="Primitive">outS</a> <a id="6392" href="Cubical.Core.Primitives.html#6264" class="Bound">u0</a><a id="6394" class="Symbol">)</a>

<a id="6397" class="Comment">-- Σ-types</a>
<a id="6408" class="Keyword">infix</a> <a id="6414" class="Number">2</a> <a id="6416" href="Cubical.Core.Primitives.html#6426" class="Function">Σ-syntax</a>

<a id="Σ-syntax"></a><a id="6426" href="Cubical.Core.Primitives.html#6426" class="Function">Σ-syntax</a> <a id="6435" class="Symbol">:</a> <a id="6437" class="Symbol">∀</a> <a id="6439" class="Symbol">{</a><a id="6440" href="Cubical.Core.Primitives.html#6440" class="Bound">ℓ</a> <a id="6442" href="Cubical.Core.Primitives.html#6442" class="Bound">ℓ&#39;</a><a id="6444" class="Symbol">}</a> <a id="6446" class="Symbol">(</a><a id="6447" href="Cubical.Core.Primitives.html#6447" class="Bound">A</a> <a id="6449" class="Symbol">:</a> <a id="6451" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="6456" href="Cubical.Core.Primitives.html#6440" class="Bound">ℓ</a><a id="6457" class="Symbol">)</a> <a id="6459" class="Symbol">(</a><a id="6460" href="Cubical.Core.Primitives.html#6460" class="Bound">B</a> <a id="6462" class="Symbol">:</a> <a id="6464" href="Cubical.Core.Primitives.html#6447" class="Bound">A</a> <a id="6466" class="Symbol">→</a> <a id="6468" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="6473" href="Cubical.Core.Primitives.html#6442" class="Bound">ℓ&#39;</a><a id="6475" class="Symbol">)</a> <a id="6477" class="Symbol">→</a> <a id="6479" href="Cubical.Core.Primitives.html#1259" class="Function">Type</a> <a id="6484" class="Symbol">(</a><a id="6485" href="Cubical.Core.Primitives.html#1184" class="Primitive">ℓ-max</a> <a id="6491" href="Cubical.Core.Primitives.html#6440" class="Bound">ℓ</a> <a id="6493" href="Cubical.Core.Primitives.html#6442" class="Bound">ℓ&#39;</a><a id="6495" class="Symbol">)</a>
<a id="6497" href="Cubical.Core.Primitives.html#6426" class="Function">Σ-syntax</a> <a id="6506" class="Symbol">=</a> <a id="6508" href="Agda.Builtin.Sigma.html#166" class="Record">Σ</a>

<a id="6511" class="Keyword">syntax</a> <a id="6518" href="Cubical.Core.Primitives.html#6426" class="Function">Σ-syntax</a> <a id="6527" class="Bound">A</a> <a id="6529" class="Symbol">(λ</a> <a id="6532" class="Bound">x</a> <a id="6534" class="Symbol">→</a> <a id="6536" class="Bound">B</a><a id="6537" class="Symbol">)</a> <a id="6539" class="Symbol">=</a> <a id="6541" class="Function">Σ[</a> <a id="6544" class="Bound">x</a> <a id="6546" class="Function">∈</a> <a id="6548" class="Bound">A</a> <a id="6550" class="Function">]</a> <a id="6552" class="Bound">B</a>
</pre></body></html>